<!DOCTYPE html><html lang=ja><head><meta charset=utf-8><meta http-equiv=X-UA-Compatible content="IE=edge"><meta name=viewport content="width=device-width,initial-scale=1"><link rel=icon href=./favicon.ico><link href="https://fonts.googleapis.com/css2?family=Sarpanch&display=swap" rel=stylesheet><title>Taichi Uchida</title><script type=x-shader/x-vertex id=vs>attribute vec3 position;

    void main(void) {
        gl_Position = vec4(position, 1.0);
    }</script><script type=x-shader/x-fragment id=random_flow_fs>precision mediump float;

    uniform float time;
    uniform float range;
    uniform float seed;
    uniform float scale;
    uniform float move;
    uniform float stepThres;
    uniform vec2 resolution;

    float rand(vec2 co) {
        return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
    }

    void main() {
      vec4 color1 = vec4(vec3(0.0), 1.0);
      vec4 color2;
      vec2 st = (2.0 * gl_FragCoord.xy - resolution) / resolution;
      st = (st + 1.0) * 0.5;
      st.t *= scale;
      st.s += -rand(vec2(floor(st.s * range), seed)) * time;
      color2 = vec4(vec3(step(rand(floor(st)), stepThres)), 1.0);
      float stripe1 = step(fract(st.t + move), 0.5);
      float stripe2 = step(fract(st.t + move + 0.5), 0.5);
      gl_FragColor = color1 * stripe1 + color2 * stripe2;
    }</script><script type=x-shader/x-fragment id=mixed_color_noise_fs>precision mediump float;

    //
    // Description : Array and textureless GLSL 2D/3D/4D simplex 
    //               noise functions.
    //      Author : Ian McEwan, Ashima Arts.
    //  Maintainer : stegu
    //     Lastmod : 20110822 (ijm)
    //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
    //               Distributed under the MIT License. See LICENSE file.
    //               https://github.com/ashima/webgl-noise
    //               https://github.com/stegu/webgl-noise
    // 

    vec3 mod289(vec3 x) {
      return x - floor(x * (1.0 / 289.0)) * 289.0;
    }

    vec4 mod289(vec4 x) {
      return x - floor(x * (1.0 / 289.0)) * 289.0;
    }

    vec4 permute(vec4 x) {
        return mod289(((x*34.0)+1.0)*x);
    }

    vec4 taylorInvSqrt(vec4 r)
    {
      return 1.79284291400159 - 0.85373472095314 * r;
    }

    float snoise(vec3 v)
      { 
      const vec2  C = vec2(1.0/6.0, 1.0/3.0) ;
      const vec4  D = vec4(0.0, 0.5, 1.0, 2.0);

    // First corner
      vec3 i  = floor(v + dot(v, C.yyy) );
      vec3 x0 =   v - i + dot(i, C.xxx) ;

    // Other corners
      vec3 g = step(x0.yzx, x0.xyz);
      vec3 l = 1.0 - g;
      vec3 i1 = min( g.xyz, l.zxy );
      vec3 i2 = max( g.xyz, l.zxy );

      //   x0 = x0 - 0.0 + 0.0 * C.xxx;
      //   x1 = x0 - i1  + 1.0 * C.xxx;
      //   x2 = x0 - i2  + 2.0 * C.xxx;
      //   x3 = x0 - 1.0 + 3.0 * C.xxx;
      vec3 x1 = x0 - i1 + C.xxx;
      vec3 x2 = x0 - i2 + C.yyy; // 2.0*C.x = 1/3 = C.y
      vec3 x3 = x0 - D.yyy;      // -1.0+3.0*C.x = -0.5 = -D.y

    // Permutations
      i = mod289(i); 
      vec4 p = permute( permute( permute( 
                i.z + vec4(0.0, i1.z, i2.z, 1.0 ))
              + i.y + vec4(0.0, i1.y, i2.y, 1.0 )) 
              + i.x + vec4(0.0, i1.x, i2.x, 1.0 ));

    // Gradients: 7x7 points over a square, mapped onto an octahedron.
    // The ring size 17*17 = 289 is close to a multiple of 49 (49*6 = 294)
      float n_ = 0.142857142857; // 1.0/7.0
      vec3  ns = n_ * D.wyz - D.xzx;

      vec4 j = p - 49.0 * floor(p * ns.z * ns.z);  //  mod(p,7*7)

      vec4 x_ = floor(j * ns.z);
      vec4 y_ = floor(j - 7.0 * x_ );    // mod(j,N)

      vec4 x = x_ *ns.x + ns.yyyy;
      vec4 y = y_ *ns.x + ns.yyyy;
      vec4 h = 1.0 - abs(x) - abs(y);

      vec4 b0 = vec4( x.xy, y.xy );
      vec4 b1 = vec4( x.zw, y.zw );

      //vec4 s0 = vec4(lessThan(b0,0.0))*2.0 - 1.0;
      //vec4 s1 = vec4(lessThan(b1,0.0))*2.0 - 1.0;
      vec4 s0 = floor(b0)*2.0 + 1.0;
      vec4 s1 = floor(b1)*2.0 + 1.0;
      vec4 sh = -step(h, vec4(0.0));

      vec4 a0 = b0.xzyw + s0.xzyw*sh.xxyy ;
      vec4 a1 = b1.xzyw + s1.xzyw*sh.zzww ;

      vec3 p0 = vec3(a0.xy,h.x);
      vec3 p1 = vec3(a0.zw,h.y);
      vec3 p2 = vec3(a1.xy,h.z);
      vec3 p3 = vec3(a1.zw,h.w);

    //Normalise gradients
      vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
      p0 *= norm.x;
      p1 *= norm.y;
      p2 *= norm.z;
      p3 *= norm.w;

    // Mix final noise value
      vec4 m = max(0.6 - vec4(dot(x0,x0), dot(x1,x1), dot(x2,x2), dot(x3,x3)), 0.0);
      m = m * m;
      return 42.0 * dot( m*m, vec4( dot(p0,x0), dot(p1,x1), 
                                    dot(p2,x2), dot(p3,x3) ) );
      }
      
    float rand(vec2 co) {
        return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
    }
      
    uniform float time;
    uniform float scale;
    uniform float noiseScale;
    uniform float wheel;
    uniform vec2 resolution;
      
    void main()
    {
      vec2 st = (2.0 * gl_FragCoord.xy - resolution) / min(resolution.x, resolution.y);
      st *= scale;
      float transform = max(0.0, wheel) + time;
      float noise1 = snoise(vec3(st, transform)) * noiseScale;
      float noise2 = snoise(vec3(noise1));
      float value = noise2;
      
      float thres;
      vec3 col;
      
      vec4 color1 = vec4(0.0, 0.0, 0.0, 0.0);
      vec4 color2 = vec4(0.0, 0.0902, 0.498, 0.2);
      vec4 color3 = vec4(1.0, 1.0, 1.0, 0.2118);
      vec4 color4 = vec4(0.0, 0.7333, 1.0, 0.4);
      vec4 color5 = vec4(0.0, 0.0, 0.0, 0.6);

      // color1 = vec4(0.0, 0.0, 0.0, 0.0);
      // color2 = vec4(0.0, 0.9, 1.0, 0.2);
      // color3 = vec4(1.0, 0.0, 0.4, 0.21);
      // color4 = vec4(1.0, 0.0, 0.0, 0.4);
      // color5 = vec4(1.0, 1.0, 1.0, 0.6);

      if (value <= color1.a) {
        col = color1.rgb;
      }
        
      if (value > color1.a && value <= color2.a) {
        thres = (value - color1.a) / (color2.a - color1.a);
        col = mix(color1.rgb, color2.rgb, thres);
      }
      
      if (value > color2.a && value <= color3.a) {
        thres = (value - color2.a) / (color3.a - color2.a);
        col = mix(color2.rgb, color3.rgb, thres);
      }
      
      if (value > color3.a && value <= color4.a) {
        thres = (value - color3.a) / (color4.a - color3.a);
        col = mix(color3.rgb, color4.rgb, thres);
      }
      
      if (value > color4.a && value <= color5.a) {
        thres = (value - color4.a) / (color5.a - color4.a);
        col = mix(color4.rgb, color5.rgb, thres);
      }
      
      if (value > color5.a) {
        col = color5.rgb;
      }
      
      gl_FragColor = vec4(col, 1.0);
    }</script><link href=/Portfolio/css/chunk-107b4e83.246cb5d2.css rel=prefetch><link href=/Portfolio/css/chunk-6296cdc6.b221fd22.css rel=prefetch><link href=/Portfolio/css/chunk-74b872ec.28850e9c.css rel=prefetch><link href=/Portfolio/js/chunk-107b4e83.304a11af.js rel=prefetch><link href=/Portfolio/js/chunk-6296cdc6.f676cd94.js rel=prefetch><link href=/Portfolio/js/chunk-74b872ec.3d714d84.js rel=prefetch><link href=/Portfolio/css/app.3d0555b3.css rel=preload as=style><link href=/Portfolio/js/app.80c2404b.js rel=preload as=script><link href=/Portfolio/js/chunk-vendors.eff61ac6.js rel=preload as=script><link href=/Portfolio/css/app.3d0555b3.css rel=stylesheet></head><body><noscript><strong>We're sorry but portfolio doesn't work properly without JavaScript enabled. Please enable it to continue.</strong></noscript><div id=app></div><script src=/Portfolio/js/chunk-vendors.eff61ac6.js></script><script src=/Portfolio/js/app.80c2404b.js></script></body></html><style>body {
  margin: 0;
  overflow: hidden;
}</style>